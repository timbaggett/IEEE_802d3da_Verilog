/**********************************************************************/
/*                          IEEE_802_3_2022                           */
/**********************************************************************/
/*                                                                    */
/*        Module: IEEE_802_3_2022_PLS.v                               */
/*        Date:   04/04/2025                                          */
/*                                                                    */
/**********************************************************************/

`timescale 1ns/100fs

/*                                                                    */
/* Define the  default macro values for the default case  when  there */
/* is  a  MAC, PLS and  MII  and no other sublayers. When  there  are */
/* additional sublayers, or  when  the PLS is being used with the MAC */
/* Merge sublayer, the macro values must be defined appropriately.    */
/*                                                                    */

`ifndef MAC 
`define MAC MAC 
`endif

`ifndef PLS 
`define PLS PLS
`endif

/*                                                                    */
/* 6. Physical Signaling (PLS) service specifications                 */
/*                                                                    */
/* The  services provided  by the  PLS  sublayer allow the  local MAC */
/* sublayer  entity to exchange  data bits (PLS data_units) with peer */
/* MAC sublayer entities.                                             */
/*                                                                    */

module `PLS();

`include "Clause_6/code/IEEE_802_3_2018_PLS_param.v"

/*                                                                    */
/* 6.3 Detailed service specification                                 */
/*                                                                    */
/* 6.3.1 Peer-to-peer service primitives                              */
/*                                                                    */
/* 6.3.1.1 PLS_DATA.request                                           */
/*                                                                    */
/* 6.3.1.1.1 Function                                                 */
/*                                                                    */
/* This primitive defines the transfer of data  from the MAC sublayer */
/* to the local PLS entity.                                           */
/*                                                                    */
/* 6.3.1.1.2 Semantics of the service primitive                       */
/*                                                                    */
/* The primitive shall provide the following parameters:              */
/* PLS_DATA.request (OUTPUT_UNIT)                                     */
/*                                                                    */
/* The OUTPUT_UNIT  parameter can take on one  of  three values: ONE, */
/* ZERO,  or  DATA_COMPLETE  and represent  a  single  data bit.  The */
/* DATA_COMPLETE  value  signifies  that  the  Media  Access  Control */
/* sublayer has no more data to output.                               */
/*                                                                    */
/* 6.3.1.1.3 When generated                                           */
/* This primitive  is  generated by the MAC sublayer  to request  the */
/* transmission of  a single data  bit on the physical  medium  or to */
/* stop transmission.                                                 */
/*                                                                    */
/* 6.3.1.1.4 Effect of receipt                                        */
/*                                                                    */
/* The receipt of this primitive will  cause the PLS entity to encode */
/* and transmit either a single data bit or tocease transmission.     */
/*                                                                    */

event       PLS_DATA_request;
event       PLS_DATA_request_serviced;

reg[1:0]    OUTPUT_UNIT;
reg[247:0]  PLS_DATA_request_ASCII;

always @(`MAC.sendBit)
begin

    #0.0001;

    if(`MAC.TransmitBit.bitParam == ZERO)
    begin
        OUTPUT_UNIT = ZERO;
        PLS_DATA_request_ASCII = "PLS_DATA.request(ZERO)";
    end
    else if(`MAC.TransmitBit.bitParam == ONE)
    begin
        OUTPUT_UNIT = ONE;
        PLS_DATA_request_ASCII = "PLS_DATA.request(ONE)";
    end
    else 
    begin
        OUTPUT_UNIT = UNKNOWN;
        PLS_DATA_request_ASCII = "PLS_DATA.request(UNKNOWN)";
    end

    -> PLS_DATA_request;

    @(PLS_DATA_request_serviced);

    -> `MAC.bitSent;

end

always @(negedge `MAC.transmitting)
begin

    #0.0001;

    OUTPUT_UNIT = DATA_COMPLETE;
    PLS_DATA_request_ASCII = "PLS_DATA.request(DATA_COMPLETE)";

    -> PLS_DATA_request;

    @(PLS_DATA_request_serviced);

end

/* 6.3.1.2 PLS_DATA.indication                                        */
/*                                                                    */
/* 6.3.1.2.1 Function                                                 */
/*                                                                    */
/* This  primitive defines the transfer of data from the PLS sublayer */
/* to the MAC sublayer.                                               */
/*                                                                    */
/* 6.3.1.2.2 Semantics of the service primitive                       */
/*                                                                    */
/* The semantics of the service primitive are as follows:             */
/* PLS_DATA. indicate (INPUT_UNIT)                                    */
/*                                                                    */
/* The  INPUT_UNIT  parameter  can   take  one  of  two  values  each */
/* representing a single bit: ONE or ZERO.                            */
/*                                                                    */
/* 6.3.1.2.3 When generated                                           */
/*                                                                    */
/* The PLS_DATA.indication is  generated to all MAC sublayer entities */
/* in the network after aPLS_DATA.request is issued.                  */
/*                                                                    */
/* NOTE—In  half duplex mode, an  indication is also presented to the */
/* MAC entity that issued the request.                                */
/*                                                                    */

event      PLS_DATA_indication;
reg        INPUT_UNIT;
reg[223:0] PLS_DATA_indication_ASCII;

always @(PLS_DATA_indication)
begin
    if (INPUT_UNIT == ZERO)
    begin
        `MAC.ReceiveBit.receive_bit_value = `MAC.zero;
        PLS_DATA_indication_ASCII = "PLS_DATA.indication(ZERO)";
    end
    else if (INPUT_UNIT == ONE)
    begin
        `MAC.ReceiveBit.receive_bit_value = `MAC.one;
        PLS_DATA_indication_ASCII = "PLS_DATA.indication(ONE)";
    end
    else
    begin
        `MAC.ReceiveBit.receive_bit_value = 1'bx;
        PLS_DATA_indication_ASCII = "PLS_DATA.indication(UNKNOWN)";
    end

    -> `MAC.PLS_DATA_indication;
end

/*                                                                    */
/* 6.3.2 Sublayer-to-sublayer service primitives                      */
/*                                                                    */
/* 6.3.2.1 PLS_CARRIER.indication                                     */
/*                                                                    */
/* 6.3.2.1.1 Function                                                 */
/*                                                                    */
/* This  primitive  transfers  the status  of  the  activity  on  the */
/* physical medium from the PLS sublayer to the MACsublayer.          */
/*                                                                    */
/* 6.3.2.1.2 Semantics of the service primitive                       */
/*                                                                    */
/* The semantics of the primitive are as follows:                     */
/*                                                                    */
/* PLS_CARRIER.indication (CARRIER_STATUS)                            */
/*                                                                    */
/* The  CARRIER_STATUS   parameter  can  take   one  of  two  values: */
/* CARRIER_ON or  CARRIER_OFF.  The  CARRIER_ON value  indicates that */
/* the  DTE  Physical  Layer  had  received  an  input  message  or a */
/* signal_quality_error  message from the MAU. The  CARRIER_OFF value */
/* indicates that  the DTE  Physical Layer had received an input_idle */
/* message and is  not receiving an SQE signal_quality_error  message */
/* from the MAU.                                                      */
/*                                                                    */

event      PLS_CARRIER_indication;
reg        CARRIER_STATUS;
reg[295:0] PLS_CARRIER_indication_ASCII;

always@(PLS_CARRIER_indication)
begin

    if(CARRIER_STATUS == CARRIER_OFF)
    begin
        `MAC.carrierSense            = false;
        PLS_CARRIER_indication_ASCII = "PLS_CARRIER.indication(CARRIER_OFF)";
    end

    else if(CARRIER_STATUS == CARRIER_ON)
    begin
        `MAC.carrierSense            = true;
        PLS_CARRIER_indication_ASCII = "PLS_CARRIER.indication(CARRIER_ON)";
    end

    else
    begin
        `MAC.carrierSense            = UNKNOWN;
        PLS_CARRIER_indication_ASCII = "PLS_CARRIER.indication(UNKNOWN)";
    end

end


/*                                                                    */
/* 6.3.2.2 PLS_SIGNAL.indication                                      */
/*                                                                    */
/* 6.3.2.2.1 Function                                                 */
/*                                                                    */
/* This primitive transfers the status of  the Physical Layer  signal */
/* quality from the PLS sublayer to the MAC sublayer.                 */
/*                                                                    */
/* 6.3.2.2.2 Semantics of the service primitive                       */
/*                                                                    */
/* The semantics of the service primitive are as follows:             */
/*                                                                    */
/* The  SIGNAL_STATUS   parameter   can   take  one  of  two  values: */
/* SIGNAL_ERROR or NO_SIGNAL_ERROR. The  SIGNAL_ERROR value indicates */
/* to    the     MAC  sublayer  that   the  PLS  has      received  a */
/* signal_quality_error message from  the  MAU.  The  NO_SIGNAL_ERROR */
/* value  indicates     that    the  PLS  has     ceased  to  receive */
/* signal_quality_error messages from the MAU.                        */
/*                                                                    */
/* 6.3.2.2.3 When generated                                           */
/*                                                                    */
/* from SIGNAL_ERROR to NO_SIGNAL_ERROR or vice versa.                */
/*                                                                    */
/* 6.3.2.2.4 Effect of receipt                                        */
/*                                                                    */
/* The PLS_SIGNAL.indication service primitive is  generated whenever */
/* SIGNAL_   STATUS  makes   a  transition    from  SIGNAL_ERROR   to */
/* NO_SIGNAL_ERROR or vice versa.                                     */
/*                                                                    */

// maint : whenever SIGNAL_ STATUS makes: remove space");

event      PLS_SIGNAL_indication;
reg[303:0] PLS_SIGNAL_indication_ASCII;
reg        SIGNAL_STATUS;

always@(PLS_SIGNAL_indication)
begin

    if(SIGNAL_STATUS == NO_SIGNAL_ERROR)
    begin
        `MAC.collisionDetect        = false;
        PLS_SIGNAL_indication_ASCII = "PLS_SIGNAL.indication(NO_SIGNAL_ERROR)";
    end

    else if(SIGNAL_STATUS == SIGNAL_ERROR)
    begin
        `MAC.collisionDetect        = true;
        PLS_SIGNAL_indication_ASCII = "PLS_SIGNAL.indication(SIGNAL_ERROR)";
    end

    else
    begin
        `MAC.collisionDetect        = UNKNOWN;
        PLS_SIGNAL_indication_ASCII = "PLS_SIGNAL.indication(UNKNOWN)";
    end

end

/*                                                                    */
/* 6.3.2.3 PLS_DATA_VALID.indication                                  */
/*                                                                    */
/* 6.3.2.3.1 Function                                                 */
/*                                                                    */
/* This  primitive  provides  a  facility  for  transferring  framing */
/* information to the MAC sublayer.                                   */
/*                                                                    */
/* 6.3.2.3.2 Semantics of the service primitive                       */
/*                                                                    */
/* The semantics of the service primitive are as follows:             */
/*                                                                    */
/* PLS_DATA_VALID.indication (DATA_VALID_STATUS)                      */
/*                                                                    */
/* 6.3.2.3.3 When generated                                           */
/*                                                                    */
/* The  PLS_DATA_VALID.indication  service   primitive  is  generated */
/* whenever the DATA_VALID_STATUS  parameter makes  a transition from */
/* DATA_VALID to DATA_NOT_VALID or vice versa.                        */
/*                                                                    */
/* 6.3.2.3.4 Effect of receipt                                        */
/*                                                                    */
/* The  effect  of receipt of  this primitive by  the MAC sublayer is */
/* not specified in this clause.                                      */
/*                                                                    */

event      PLS_DATA_VALID_indication;
reg        DATA_VALID_STATUS;
reg[327:0] PLS_DATA_VALID_indication_ASCII;

always@(PLS_DATA_VALID_indication)
begin

    if(DATA_VALID_STATUS == DATA_NOT_VALID)
    begin
        `MAC.receiveDataValid           = false;
        PLS_DATA_VALID_indication_ASCII = "PLS_DATA_VALID.indication(DATA_NOT_VALID)";
    end

    else if(DATA_VALID_STATUS == DATA_VALID)
    begin
        `MAC.receiveDataValid           = true;
        PLS_DATA_VALID_indication_ASCII = "PLS_DATA_VALID.indication(DATA_VALID)";
    end

    else
    begin
        `MAC.receiveDataValid           = UNKNOWN;
        PLS_DATA_VALID_indication_ASCII = "PLS_DATA_VALID.indication(UNKNOWN)";
    end

end

endmodule
